#!/usr/bin/env fish

function inf; set_color green ; echo >&1 "INFO : $argv"; set_color normal; end
function err; set_color red   ; echo >&1 "ERROR: $argv"; set_color normal; end
function wrn; set_color yellow; echo >&1 "WARN : $argv"; set_color normal; end

function link -a src dst
  if test -e $dst; and not test -L $dst
    wrn "`$dst` already exists and it is not a symlink, ignoring"
    return
  end

  rm -f $dst; \
    and ln -sf $src $dst; \
    and inf "`$dst` was properly updated"
end

mkdir -p "$HOME/.config"

for file in $PWD/* $PWD/config/*
  set -l name (string replace "$PWD/" "" $file)

  switch $name
    case install README.md
      inf "ignoring `$name`"

    case "config"
      continue

    case "disabled"
      continue

    case "bin"
      link $file "$HOME/$name"

    case "*"
      link $file "$HOME/.$name"
  end
end

